tree("Root")
	parallel
		repeat mute tree("Patrol")
		repeat mute tree("Dead")
tree("Patrol")
	fallback
		tree("Flee")
		while not IsHealthLessThan(40.0)
			fallback
				tree("Attack")
				tree("LookAround")
				tree("Wander")
		
tree("Attack")
	sequence
		SeePlayer
		TargetPlayer
		LookAtTarget
		fallback
			while ShotLinedUp
				sequence
					Wait(0.3)
					Fire
			while not ShotLinedUp
				sequence
					SetTargetDestination
					MoveToDestination


tree("Dead")
	while IsHealthLessThan(0.1)
		Explode

tree("Flee")
	sequence
		IsHealthLessThan(40.0)
		mute
			while IsHealthLessThan(40.0)
				sequence
					mute 
						while InDanger(50.0)
							sequence
								TakeCover
								MoveToDestination
		not IsHealthLessThan(39.0)
		DebugLog("Returning To Last Target Position")
		SetTargetDestination
		MoveToDestination

		/*
		mute
			while IsHealthLessThan(40.0)
				fallback
					while InDanger(50.0)
						sequence
							TakeCover
							MoveToDestination
					//Wait(1)
		
		sequence
			not IsHealthLessThan(40.0)
			DebugLog("Returning To Last Target Position")
			SetTargetDestination
			MoveToDestination
			*/
			
		

tree("LookAround")
	while not IsHealthLessThan(40.0)
		while not SeePlayer
			sequence
				random
					Turn(90.0)
					Turn(-90.0)
					Turn(145.0)
					Turn(-27.0)
				LookAtTarget
				WaitRandom(2.0,5.0)
				random
					Succeed
					Fail

tree("Wander")
	while not SeePlayer
		sequence
			PickRandomDestination
			MoveToDestination
